{


# macros that translate directly to builtin commands (but know the amount of arguments)
!putchar(c){c .putchar}
!add(a b){a b .add}
!mult(a b){a b .mult}
!div(a b){a b .div}
!mod(a b){a b .mod}
!bitand(a b){a b .bitand}
!bitor(a b){a b .bitor}
!shiftup(a b){a b .shiftup}
!shiftdown(a b){a b .shiftdown}
!exit(code){code .exit}
!jumpifz(condition to){condition to .jumpifz}
!setstack(to){to .setstack}
!not(a){a .not}
!moveto(value ptr){value ptr .moveto}
!memmove(amount src dest){amount src dest .memmove}

# macros that almost translate directly to builtins
!minus(val){val .neg}
!isnegative(a){a .negative}
!deref (ptr) {ptr .movefrom}
!setref(ptr value){
	ptr
	value
	.swap
	.moveto
}
!dup2 {
	.dup
	.dup
	.drop
	.drop
	.drop
	.dup
	.undrop
	.swap
	.undrop
}
!jump(to){jumpifz(0 to)}
!jumpif(condition to) jumpifz(not(condition) to)

# other macros

!ignore(arg) {}
!macrovar(label){
	# a space of memory in the middle of the code that can be used as a global variable
	.push
	label
	[0]
	.drop
}
!dupsecond {
	.swap
	.dup
	.drop
	.swap
	.undrop
}
!dowhile (body condition)(:whilestart) {
	:whilestart
	body
	jumpifz(not(condition) whilestart)
}
!if(condition body)(:ifend) {
	jumpifz(condition ifend)
	body
	:ifend
}
!and(a b) (:end){
	a
	jumpifz(.dup end)
	.drop
	b
	:end
}
!or(a b) (:end){
	a
	jumpif(.dup end)
	.drop
	b
	:end
}
!ifelse(condition ifbody elsebody)(:elsepart :ifend) {
	jumpifz(condition elsepart)
	ifbody
	jump(ifend)
	:elsepart
	elsebody
	:ifend
}
!while (condition body) {
	if(condition {
		dowhile (body condition)
	})
}
!sub(a b){
	add(a minus(b))
}
!lt (left right){
	isnegative(sub(left right))
}
!gt(left right){
	left
	right
	.swap
	isnegative(sub({ } { }))
}
!leq(left right){
	not(gt(left right))
}
!geq(left right){
	not(lt(left right))
}
!neq(a b){
	sub(a b)
}
!eq (a b){
	not(sub(a b))
}
!varptr(id) {
	add(deref(@funcmem) id)
}
!setvar(id value){
	setref(varptr(id) value)
}
!getvar(id){
	deref(varptr(id))
}

!addto (pos val){
	setref(pos add(deref(.dup) val))
}

!print(string)(:stringstart :stringend){
	stringstart
	while( neq(.dup stringend) {
		putchar(deref(.dup))
		add({ } 1)
	})
	jump(stringend)
	:stringstart
	string
	:stringend
	.drop
}
!putnl putchar('\n)

!println(string){
	print(string)
	putnl
}
!putint(val)(:end){
	val
	if(not(.dup){
		putchar(48)
		.drop
		jump(end)
	})
	if(isnegative(.dup){
		putchar(45)
		.neg
	})
	0
	.swap
	while(.dup {
		add(mod(.dup 10) 48)
		.swap
		div({ } 10)
	})
	.drop
	while(.dup {
		.putchar
	})
	.drop
	:end
}
!printint(val){
	val
	putchar(40)
	putint({ })
	putchar(41)
}

!calls(fun)(:returnpos){
	returnpos
	jump(fun)
	:returnpos
}
!call(fun args){
	args
	calls(fun)
}


!endfunc()(:memtmp :returntmp){
	macrovar(:memtmp)
	macrovar(:returntmp)
	# store old stack and old mem location location in temporary globals
	setref(memtmp getvar(-1))
	setref(@calltmp getvar(-2))
	#calculate and store amount of return values
	.getstack
	minus(add(add(deref(@funcmem) getvar(1)) 2))
	.add
	returntmp
	.moveto
	
	# move return values to top of regular stack
	# source location start
	.getstack
	minus(deref(returntmp))
	.add
	# amount of return values
	deref(returntmp)
	.swap
	# destination location start
	sub(deref(@funcmem) add(getvar(0) 2))
	# remember destination location for later (because getvar(0) might get overwritten)
	.dup
	@funcmem
	.moveto
	# actually move the return values
	.memmove
	
	# move stack to end of return values
	setstack(add(deref(@funcmem) deref(returntmp)))
	
	# recall old stack location
	setref(@funcmem deref(memtmp))
}
!returns {
	endfunc
	# return to previous position
	jump(deref(@calltmp))
}

!function(label nargs nvars body){
	label
	# remember old stack location
	deref(@funcmem)
	
	# remember current stack location
	.getstack
	@funcmem
	.moveto
	if(geq(deref(@funcmem) deref(@heapstart)){
		println("Stack Overflow!")
		exit(-1)
	})
	
	# remember argument number
	nargs
	# remember number of local variables
	nvars
	
	# reserve space for local variables
	.dup
	.getstack
	.add
	.setstack
	.drop
	# execute function body
	body
	
	returns
}
!return(val){
	val
	returns
}
!tailcalls(fun){
	endfunc
	deref(@calltmp)
	jump(fun)
}
!tailcall(fun val){
	val
	tailcalls(fun)
}
!arrset(arr index val){
	setref(add(arr index) val)
}
!arrget(arr index){
	deref(add(arr index))
}
!printmem(ptr size){
	ptr
	size
	dup2
	.add
	.swap
	.drop
	.swap
	while(neq(dup2 { }) {
		putchar(deref(.dup))
		add({ } 1)
	})
	.drop
	.drop
}



!log2(n){
	n
	shiftdown(.dup 16)
	ifelse(.dup {
		.swap
		.drop
		shiftdown(.dup 8)
		ifelse(.dup {
			.swap
			.drop
			add(deref(add(@log2table256 {})) 24)
		}{
			.drop
			add(deref(add(@log2table256 {})) 16)
		})
	}{
		.drop
		shiftdown(.dup 8)
		ifelse(.dup {
			.swap
			.drop
			add(deref(add(@log2table256 {})) 8)
		}{
			.drop
			deref(add({} @log2table256))
		})
	})
}













# actual code part

# some initializer metadata
[
0
@codestart # initial location of the code pointer
@codeend # initial location of the stack pointer
]

# place where the code starts executing
:codestart
addto(@heapstart 4096) # reserve 4096 words space for the stack
setref(@heapend deref(@heapstart))

# end with as exit code the return value from main
exit(call(@main { }))



!mallocnew(amount) (:end){
	add(2 deref(@heapend)) # the eventual return value
	macrovar(:end)
	setref(end add(add(amount 3) deref(@heapend)))
	setref(deref(@heapend) deref(end))
	arrset(deref(@heapend) 1 0)
	arrset(deref(end) -1 deref(@heapend))
	setref(@heapend deref(end))
}
!calcfreeblocklist(size)(){
	add(mult(log2(size) 4) @freetable)
}
!addfreedblock(start_) (:start :blocklist) {
	macrovar(:start)
	setref(start start_)
	macrovar(:blocklist)
	setref(blocklist calcfreeblocklist(sub(deref(deref(start)) deref(start))))
	arrset(deref(start) 2 deref(blocklist))
	arrset(deref(start) 1 arrget(deref(blocklist) 1))
	arrset(arrget(deref(blocklist) 1) 2 deref(start))
	arrset(deref(blocklist) 1 deref(start))
}
!removefreeblock_u(blockstart){
	!nextinlist arrget(blockstart 1)
	!previnlist arrget(blockstart 2)
	arrset(previnlist 1 nextinlist)
	arrset(nextinlist 2 previnlist)
}

function(:malloc 1 3 {
	!amount -3
	!heapptr 2
	!end 3
	!next 4
	if (lt(getvar(amount) 1)
		# minimum size of a block is 4 words (3 meta words + 1 data word)
		setvar(amount 1)
	)
	setvar(heapptr arrget(calcfreeblocklist(add(getvar(amount) 3)) 1))
	while(neq(getvar(heapptr) @endblock) { # loop over all chunks
		if(arrget(getvar(heapptr) 1) { # chunk is free
			setvar(next deref(getvar(heapptr)))
			setvar(end add(add(getvar(heapptr) getvar(amount)) 3))
			if(leq(getvar(end) getvar(next)) {
				# enough free space found!
				removefreeblock_u(getvar(heapptr))
				if(geq(sub(getvar(next) getvar(end)) 4) {
					# still a significant free space behind this block
					# add a new free chunk
					arrset(getvar(end) 0 getvar(next))
					arrset(getvar(next) -1 getvar(end))
					setvar(next getvar(end))
					addfreedblock(getvar(end))
				})
				# claim this block
				arrset(getvar(heapptr) 0 getvar(next))
				arrset(getvar(heapptr) 1 0)
				arrset(getvar(next) -1 getvar(heapptr))
				return(add(getvar(heapptr) 2))
				
			})
		})
		setvar(heapptr arrget(getvar(heapptr) 1))
	})
	# no free chunk found that's large enough; allocate some new memory
	mallocnew(getvar(amount))
})
!malloc(amount) call(@malloc amount)
!alloc_block_start(alloc_ptr) add(alloc_ptr -2)
!alloc_block_size(block_start){
	block_start
	.dup
	.movefrom
	.swap
	.neg
	.add
}
!allocatedsize(alloc_ptr){
	add(alloc_block_size(alloc_block_start(alloc_ptr)) -3)
}
!is_free_block(block_start){
	and(lt(block_start deref(@heapend)) arrget(block_start 1))
}
function(:free 1 1 {
	!start -3
	!end 2
	setvar(end deref(getvar(start)))
	if (and(neq(getvar(end) deref(@heapend)) arrget(getvar(end) 1)) {
		# next block is free
		# remove next block from list of free blocks
		removefreeblock_u(getvar(end))
		# merge with next block
		setvar(end deref(getvar(end)))
		setref(getvar(start) getvar(end))
		arrset(getvar(end) -1 getvar(start))
	})
	if(and(neq(getvar(start) deref(@heapstart)) arrget(arrget(getvar(start) -1) 1)){
		# previous block is free
		# merge with previous block
		setvar(start arrget(getvar(start) -1))
		
		# remove next block from list of free blocks
		removefreeblock_u(getvar(start))
		
		setref(getvar(start) getvar(end))
		arrset(getvar(end) -1 getvar(start))
	})
	addfreedblock(getvar(start))
})
!free(ptr) call(@free add(ptr -2))
function(:realloc 2 2 {
	!ptr -4
	!amount -3
	!new 2
	!oldamount 3
	
	setvar(oldamount sub(deref(getvar(ptr)) getvar(ptr)))
	
	while(gt(getvar(amount) sub(deref(getvar(ptr)) getvar(ptr))) {
		ifelse(is_free_block(deref(getvar(ptr))) {
			# next block is free; merge it
			removefreeblock_u(deref(getvar(ptr)))
			setref(getvar(ptr) deref(deref(getvar(ptr))))
			arrset(deref(getvar(ptr)) -1 getvar(ptr))
		} {
			# no free block following; allocate new
			addto(varptr(ptr) 2)
			setvar(new malloc(add(getvar(amount) -3)))
			memmove(add(getvar(oldamount) -3) getvar(ptr) getvar(new))
			free(getvar(ptr))
			return(getvar(new))
		})
	})
	getvar(ptr)
})
!realloc(ptr amount) call(@realloc {add(ptr -2) add(amount 3)})

!clonestruct(src size)(:clone){
	macrovar(:clone)
	setref(clone malloc(size))
	memmove(size src deref(clone))
	deref(clone)
}

!pair(first second){
	malloc(2)
	setref(.dup first)
	arrset(.dup 1 second)
}
!triple(first second third){
	malloc(3)
	setref(.dup first)
	arrset(.dup 1 second)
	arrset(.dup 2 third)
}
!quadruple(first second third fourth){
	malloc(4)
	setref(.dup first)
	arrset(.dup 1 second)
	arrset(.dup 2 third)
	arrset(.dup 3 fourth)
}
!quintuple(first second third fourth fifth){
	malloc(5)
	setref(.dup first)
	arrset(.dup 1 second)
	arrset(.dup 2 third)
	arrset(.dup 3 fourth)
	arrset(.dup 4 fifth)
}


!vecnew pair(0 malloc(8))

function(:vecresize 2 1 {
	!vec -4
	!newsize -3
	!allocsize 2
	
	arrset(getvar(vec) 0 getvar(newsize))
	setvar(allocsize allocatedsize(arrget(getvar(vec) 1)))
	if (lt(getvar(allocsize) getvar(newsize)){
		setvar(allocsize mult(getvar(allocsize) 2))
		if (lt(getvar(allocsize) getvar(newsize)){
			setvar(allocsize getvar(newsize))
		})
		arrset(getvar(vec) 1 realloc(arrget(getvar(vec) 1) getvar(allocsize)))
	})
})
!vecresize(vec newsize) call(@vecresize {vec newsize})
!veclength(vec){deref(vec)}
!vecget(vec idx){arrget(arrget(vec 1) idx)}
!vecset(vec idx val){arrset(arrget(vec 1) idx val)}
function(:vecappend 2 0 {
	!vec -4
	!val -3
	vecresize(getvar(vec) add(veclength(getvar(vec)) 1))
	vecset(getvar(vec) add(veclength(getvar(vec)) -1) getvar(val))
})
!vecappend(vec val) call(@vecappend {vec val})
function(:vecclone 1 1 {
	!vec -3
	!new 2
	setvar(new vecnew)
	vecresize(getvar(new) veclength(getvar(vec)))
	memmove(veclength(getvar(vec)) arrget(getvar(vec) 1) arrget(getvar(new) 1))
	getvar(new)
})
!vecclone(vec) call(@vecclone vec)
function(:vecextend 2 0 {
	!vec -4
	!src -3
	vecresize(getvar(vec) add(veclength(getvar(vec)) veclength(getvar(src))))
	memmove(veclength(getvar(src)) arrget(getvar(src) 1) arrget(getvar(vec) 1))
})
!vecextend(vec src) call(@vecextend {vec src})
function(:veceq 2 1 {
	!vec1 -4
	!vec2 -3
	!i 2
	ifelse(neq(veclength(getvar(vec1)) veclength(getvar(vec2))){
		0
	}{
		setvar(i 0)
		while(neq(getvar(i) veclength(getvar(vec1))) {
			if(neq(vecget(getvar(vec1) getvar(i)) vecget(getvar(vec2) getvar(i))) {
				return(0)
			})
			addto(varptr(i) 1)
		})
		1
	})
})
!veceq(vec1 vec2) {call(@veceq {vec1 vec2})}
		
!veciter(vec item body){
	vec
	arrget(.dup 1) # pointer to start
	.swap
	arrget(.dup 1) # another pointer to start
	.swap
	.movefrom # number of elements
	.add # pointer to end
	.swap
	while(neq(dup2 {}) {
		moveto(deref(.dup) item)
		body
		add({} 1)
	})
	.drop
	.drop
}

!vecfree(vec){
	free(arrget(vec 1))
	free(vec)
}

!printstr(vecstr){
	vecstr
	.dup
	arrget({ } 1)
	.swap
	.movefrom
	printmem({ } { })
}

function(:hashvec 1 2{
	!vec -3
	!letter 2
	!h 3
	setvar(h 448199)
	veciter(getvar(vec) varptr(letter) {
		setvar(h mult(add(getvar(h) getvar(letter)) 1893962933))
	})
	getvar(h)
})
!hashvec(vec) call(@hashvec vec)

!linkedmapnew 0
!linkedmapinsert(map key value){
	quadruple(map key value hashvec(key))
}
!linkedmapget(map key default) call(@linkedmapget {map key default})
function(:linkedmapget 3 1 {
	!map -5
	!key -4
	!default -3
	!h 2
	setvar(h hashvec(getvar(key)))
	while(getvar(map){
		if(and(eq(getvar(h) arrget(getvar(map) 3)) veceq(getvar(key) arrget(getvar(map) 1))) {
			return(arrget(getvar(map) 2))
		})
		setvar(map deref(getvar(map)))
	})
	getvar(default)
})


!treemap_left 0
!treemap_right 1
!treemap_key 2
!treemap_hash 3
!treemap_value 4
!treemapnew 0
function(:treemapget 3 1 {
	!map -5
	!key -4
	!default -3
	!h 2
	setvar(h hashvec(getvar(key)))
	while(getvar(map){
		if(and(eq(getvar(h) arrget(getvar(map) treemap_hash)) veceq(getvar(key) arrget(getvar(map) treemap_key))) {
			return(arrget(getvar(map) treemap_value))
		})
		setvar(map arrget(
			getvar(map)
			ifelse(lt(getvar(h) arrget(getvar(map) treemap_hash))
				treemap_left
				treemap_right
			)
		))
	})
	getvar(default)
})
!treemapget(map key default) call(@treemapget {map key default})
!treemapinsert_(map key value hashed) call(@treemapinsert {map key value hashed})
!treemapinsert(map key value) treemapinsert_(map key value hashvec(key))
function(:treemapinsert 4 1 {
	!map -6
	!key -5
	!value -4
	!h -3
	if(not(getvar(map))
		return(quintuple(0 0 getvar(key) getvar(h) getvar(value)))
	)
	if(and(eq(getvar(h) arrget(getvar(map) treemap_hash)) veceq(getvar(key) arrget(getvar(map) treemap_key))) {
		return(quintuple(
			arrget(getvar(map) treemap_left)
			arrget(getvar(map) treemap_right)
			getvar(key)
			getvar(h)
			getvar(value)
		))
	})
	
	ifelse(lt(getvar(h) arrget(getvar(map) treemap_hash)){
		return(quintuple(
			treemapinsert_(arrget(getvar(map) treemap_left) getvar(key) getvar(value) getvar(h))
			arrget(getvar(map) treemap_right)
			arrget(getvar(map) treemap_key)
			arrget(getvar(map) treemap_hash)
			arrget(getvar(map) treemap_value)
		))
	} {
		return(quintuple(
			arrget(getvar(map) treemap_left)
			treemapinsert_(arrget(getvar(map) treemap_right) getvar(key) getvar(value) getvar(h))
			arrget(getvar(map) treemap_key)
			arrget(getvar(map) treemap_hash)
			arrget(getvar(map) treemap_value)
		))
	})
})

!hashmapsize 16
!hashmapnew{
	pair(16 malloc(16))
	arrget(.dup 1)
	add(.dup 16)
	.swap
	while(neq(dup2 {}) {
		setref(.dup 0)
		add({} 1)
	})
	.drop
	.drop
}

function(:hashmapinsert 3 1 {
	!map -5
	!key -4
	!value -3
	!location 2
	setvar(location add(arrget(getvar(map) 1) mod(hashvec(varptr(key)) deref(getvar(map)))))
	if(not(deref(getvar(location))) {
		setref(getvar(location) vecnew)
	})
	linkedmapinsert(deref(getvar(location)) getvar(key) getvar(value))
})
!hashmapinsert(map key value) call(@hashmapinsert {map key value})

function(:hashmapcopy 1 2 {
	!map -3
	!new 2
	!i 3
	setvar(new pair(deref(getvar(map)) malloc(deref(getvar(map)))))
	setvar(i 0)
	while(neq(getvar(i) deref(getvar(map))) {
		arrset(arrget(getvar(new) 1) getvar(i) vecclone(arrget(arrget(getvar(map) 1) getvar(i))))
	})
	getvar(new)
})
!hashmapcopy(map) call(@hashmapcopy value)


function(:readinput 0 4 {
	!text 2
	!char 3
	setvar(text vecnew())
	setvar(char .getchar)
	while(not(isnegative(getvar(char))){
		vecappend(getvar(text) getvar(char))
		setvar(char .getchar)
	})
	getvar(text) # return a linked list of letters
})

!Tokentype_ident 'I
!Tokentype_num 'N
!Tokentype_str 'S
!Tokentype_macro '!
!Tokentype_label ':
!Tokentype_reference '@
!Tokentype_builtin '.

!Token_type(token) deref(token)
!Token_body(token) arrget(token 1)
!Token_linenum(token) arrget(token 2)

!isspace(char) or(or(or(eq(char '\t) eq(char '\n)) eq(char '\r)) eq(char ' ))
!isalpha(char) or(and(geq(char 'A) leq(char 'Z)) and(geq(char 'a) leq(char 'z)))
!isidentchar(char) or(isalpha(char) eq(char '_))
!isdecimal(char) and(geq(char '0) leq(char '9))


function(:tokenize 4 4 {
	!letters -6
	!index -5
	!linenum -4
	!parsed -3
	!char 2
	!token 3
	!numvalue 4
	!textvalue 5
	
	!callnext tailcall(@tokenize {getvar(letters) getvar(index) getvar(linenum) getvar(parsed)})
	!notatend neq(getvar(index) veclength(getvar(letters)))
	!nextchar vecget(getvar(letters) getvar(index))
	!addtoken(type value) vecappend(getvar(parsed) triple(type value getvar(linenum)))
	
	if(not(notatend) {
		return(getvar(parsed))
	})
	setvar(char nextchar)
	addto(varptr(index) 1)
	if(isspace(getvar(char)) {
		if(eq(getvar(char) '\n)
			addto(varptr(linenum) 1)
		)
		callnext
	})
	if(eq(getvar(char) '#) {
		# start of a comment
		while(and(
			notatend
			neq(getvar(char) '\n) # end of a comment
		){
			setvar(char nextchar)
			addto(varptr(index) 1)
		})
		addto(varptr(linenum) 1)
		callnext
	})
	if(isdecimal(getvar(char)) {
		# positive number
		setvar(numvalue sub(getvar(char) '0))
		while( and(notatend isdecimal(nextchar)) {
			setvar(char nextchar)
			setvar(numvalue mult(getvar(numvalue) 10))
			addto(varptr(numvalue) sub(getvar(char) '\n))
			addto(varptr(index) 1)
		})
		addtoken(Tokentype_num getvar(numvalue))
		callnext
	})
	if(and(eq(getvar(char) '-) and(notatend isdecimal(nextchar))) {
		# negative number
		setvar(numvalue add(getvar(char) 0))
		while( and(notatend isdecimal(nextchar)) {
			setvar(numvalue mult(getvar(numvalue) 10))
			addto(varptr(numvalue) sub(nextchar '0))
			addto(varptr(index) 1)
		})
		setvar(numvalue minus(getvar(numvalue)))
		addtoken(Tokentype_num getvar(numvalue))
		callnext
	})
	if(isidentchar(getvar(char)) {
		setvar(textvalue vecnew)
		vecappend(getvar(textvalue) getvar(char))
		while(and(notatend or(isidentchar(nextchar) isdecimal(nextchar))) {
			vecappend(getvar(textvalue) nextchar)
			addto(varptr(index) 1)
		})
		addtoken(Tokentype_ident getvar(textvalue))
		callnext
	})
	if(or(or(or(or(or( eq(getvar(char) '() eq(getvar(char) '))) eq(getvar(char) '[)) eq(getvar(char) '])) eq(getvar(char) '{)) eq(getvar(char) '})){ # brackets or exclamation mark
		addtoken(getvar(char) getvar(char))
		callnext
	})
	if(or(or(or(eq(getvar(char) ':) eq(getvar(char) '@)) eq(getvar(char) '.)) eq(getvar(char) '!)) {
		setvar(textvalue vecnew)
		if(or(not(notatend) not(isidentchar(nextchar))) {
			print("token '")
			putchar(getvar(char))
			print("' must be followerd by a letter or underscore; not '")
			putchar(nextchar)
			print("'\n")
			jump(@tokenizeEndTooEarly)
		})
		vecappend(getvar(textvalue) nextchar)
		addto(varptr(index) 1)
		while(and(notatend or(isidentchar(nextchar) isdecimal(nextchar))) {
			vecappend(getvar(textvalue) nextchar)
			addto(varptr(index) 1)
		})
		addtoken(getvar(char) getvar(textvalue))
		callnext
	})
	
	!tokenize_char {
		ifelse (eq(nextchar '\\) {
			addto(varptr(index) 1)
			if(not(notatend){
				jump(@tokenizeEndTooEarly)
			})
			ifelse (eq(nextchar 'n)
				'\n
				ifelse (eq(nextchar 'r)
					'\r
					ifelse (eq(nextchar 't)
						'\t
						nextchar
					)
				)
			)
		} {
			nextchar
		})
	}
	if(eq(getvar(char) '') {
		if (eq(nextchar '\n)
			addto(varptr(linenum) 1)
		)
		if(not(notatend){
			jump(@tokenizeEndTooEarly)
		})
		addtoken(Tokentype_num tokenize_char)
		addto(varptr(index) 1)
		callnext
	})
	if(eq(getvar(char) '"){
		setvar(textvalue vecnew)
		while(neq(nextchar '"){
			if(not(notatend){
				jump(@tokenizeEndTooEarly)
			})
			vecappend(getvar(textvalue) tokenize_char)
			addto(varptr(index) 1)
		})
		addto(varptr(index) 1)
		addtoken(Tokentype_str getvar(textvalue))
		callnext
	})
	print("unknown token: '")
	putchar(getvar(char))
	print("'")
	printint(getvar(char))
	print(" on line ")
	putint(getvar(linenum))
	putnl
	exit(-1)
	:tokenizeEndTooEarly
	print("tokenization failed on line ")
	printint(getvar(linenum))
	putnl
	exit(-1)
})
!tokenize(letters) call(@tokenize {letters 0 1 vecnew})


!Nodetype_call 'C
!Nodetype_builtin Tokentype_builtin
!Nodetype_macro '!
!Nodetype_block 'B
!Nodetype_raw 'R
!Nodetype_num Tokentype_num
!Nodetype_reference Tokentype_reference
!Nodetype_label Tokentype_label

!Node_type(node) deref(node)
!Node_body(node) arrget(node 1)
!Node_extra(node) arrget(node 2)
!Node_free(node) call(@Node_free node)
function(:Node_free 1 1 {
	!node -3
	!item 2
	!nodeis(t) eq(Node_type(getvar(node)) t)
	!body Node_body(getvar(node))
	!extra Node_extra(getvar(node))
	ifelse(nodeis(Nodetype_call) {
		!args extra
		vecfree(body)
		veciter(args varptr(item) {
			Node_free(getvar(item))
		})
		vecfree(args)
	} ifelse(nodeis(Nodetype_macro) {
		vecfree(body)
		!macroargs arrget(Node_extra(getvar(node)) 0)
		!macrolabels arrget(Node_extra(getvar(node)) 1)
		!macrobody arrget(Node_extra(getvar(node)) 2)
		veciter(macroargs varptr(item) {
			vecfree(getvar(item))
		})
		vecfree(macroargs)
		veciter(macrolabels varptr(item) {
			vecfree(getvar(item))
		})
		vecfree(macrolabels)
		Node_free(macrobody)
		free(extra)
	} ifelse(nodeis(Nodetype_block) {
		veciter(body varptr(item) {
			Node_free(getvar(item))
		})
		vecfree(body)
	} ifelse(or(or(or(nodeis(Nodetype_builtin) nodeis(Nodetype_raw)) nodeis(Nodetype_label)) nodeis(Nodetype_reference)){
		vecfree(body)
	} ifelse(nodeis(Nodetype_num) {
		.noop
	} {
		print("Invalid node type ")
		putchar(Node_type(getvar(node)))
		printint(Node_type(getvar(node)))
		print(" encountered when freeing nodes\n")
		exit(-1)
	})))))
	free(getvar(node))
})


!Codetype_literal 'L
!Codetype_command '.
!Codetype_reference '@
!Codetype_label ':

function(:parse 2 6 {
	!tokens -4
	!index -3
	!token 2
	!body 3
	!args 4
	!text 4
	!labels 5
	!item 5
	
	!notatend neq(getvar(index) veclength(getvar(tokens)))
	!isatend not(notatend)
	!checkend(message) {
		if(isatend {
			print(message)
			print(" on line ")
			putint(Token_linenum(getvar(token)))
			putnl
			jump(@parse_failed)
		})
	}
	!returnnode(typ data extra){
		return({quadruple(typ data extra Token_linenum(getvar(token))) getvar(index)})
	}
	!nexttoken vecget(getvar(tokens) getvar(index))
	!typ Token_type(getvar(token))
	!callself {
		call(@parse{getvar(tokens) getvar(index)})
		moveto({ } varptr(index))
	}
	
	if (isatend {
		println("expected command but token stream ended")
		jump(@parse_failed)
	})
	setvar(token vecget(getvar(tokens) getvar(index)))
	addto(varptr(index) 1)
	
	if (eq(typ Tokentype_ident) {
		setvar(args vecnew)
		if(eq(Token_type(nexttoken) '() {
			addto(varptr(index) 1)
			while(neq(Token_type(nexttoken) ')) {
				vecappend(getvar(args) callself)
				checkend("no closing tag for macro call")
			})
			addto(varptr(index) 1)
		})
		returnnode(
			Nodetype_call
			vecclone(Token_body(getvar(token)))
			getvar(args)
		)
	})
	if (eq(typ Tokentype_macro) {
		setvar(args vecnew)
		checkend("macro has no body")
		if(eq(Token_type(nexttoken) '() {
			addto(varptr(index) 1)
			while(neq(Token_type(nexttoken) ')){
				if(neq(Token_type(nexttoken) Tokentype_ident) {
					println("macro argument names must be identifiers")
					jump(@parse_failed)
				})
				vecappend(getvar(args) vecclone(Token_body(nexttoken )))
				addto(varptr(index) 1)
				checkend("no closing tag for macro args")
			})
			addto(varptr(index) 1)
			checkend("macro has no body")
		})
		setvar(labels vecnew)
		if(eq(Token_type(nexttoken) '() {
			addto(varptr(index) 1)
			while(neq(Token_type(nexttoken) ')){
				if(neq(Token_type(nexttoken) Tokentype_label) {
					println("macro labels must be labels")
					jump(@parse_failed)
				})
				vecappend(getvar(labels) vecclone(Token_body(nexttoken)))
				addto(varptr(index) 1)
				checkend("no closing tag for macro labels")
			})
			addto(varptr(index) 1)
			checkend("macro has no body")
		})
		setvar(body callself)
		returnnode(
			Nodetype_macro
			vecclone(Token_body(getvar(token)))
			triple(getvar(args) getvar(labels) getvar(body))
		)
	})
	if(eq(typ '{) {
		setvar(body vecnew)
		while(neq(Token_type(nexttoken) '}){
			vecappend(getvar(body) callself)
			checkend("no closing tag for code block")
		})
		addto(varptr(index) 1)
		returnnode(
			Nodetype_block
			getvar(body)
			0
		)
	})
	if (or(or(eq(typ Tokentype_builtin) eq(typ Tokentype_label)) eq(typ Tokentype_reference)){
		returnnode(
			typ
			vecclone(Token_body(getvar(token)))
			0
		)
	})
	if(eq(typ Tokentype_num) {
		returnnode(
			Nodetype_num
			Token_body(getvar(token))
			0
		)
	})
	if (eq(typ Tokentype_str) {
		setvar(body vecnew)
		veciter(Token_body(getvar(token)) varptr(item) {
			vecappend(getvar(body) pair(Codetype_literal getvar(item)))
		})
		returnnode(
			Nodetype_raw
			getvar(body)
			0
		)
	})
	if (eq(typ '[){
		setvar(body vecnew)
		while(neq(Token_type(nexttoken) ']){
			ifelse(eq(Token_type(nexttoken) Tokentype_num)
				vecappend(getvar(body) pair(Codetype_literal Token_body(nexttoken)))
				ifelse(eq(Token_type(nexttoken) Tokentype_reference)
					vecappend(getvar(body) pair(Codetype_reference vecclone(Token_body(nexttoken))))
					ifelse(eq(Token_type(nexttoken) Tokentype_label)
						vecappend(getvar(body) pair(Codetype_label vecclone(Token_body(nexttoken))))
					{
						print("incorrect type in literal block: ")
						putchar(Token_type(nexttoken))
						putnl
						jump(@parse_failed)
					})
				)
			)
			addto(varptr(index) 1)
			checkend("no closing tag for raw block")
		})
		addto(varptr(index) 1)
		returnnode(
			Nodetype_raw
			getvar(body)
			0
		)
	})
	print("Unknown token type ")
	putchar(typ)
	printint(typ)
	putnl
	
	:parse_failed
	println("Parsing failed")
	exit(-1)
})
!parse(tokens) {
	call(@parse {tokens 0})
	.drop
}

!Command_PUSH 3

!compile_tree(code node substitutions scope) call(@compile_tree {code node substitutions scope})
function(:compile_tree 4 6 {
	!code -6
	!node -5
	!substitutions -4
	!scope -3
	!item 3
	!i 3
	!cursub 4
	!bodysubs 5
	!name 6
	!localsubs 6
	!subcode 7
	!macrodata 5
	
	!nodetype deref(getvar(node))
	
	#printint(.getstack)
	#print(" compiling a ")
	#putchar(nodetype)
	#putnl
	
	if (eq(nodetype Nodetype_builtin){
		vecappend(getvar(code) pair(Codetype_command arrget(getvar(node) 1)))
		return(getvar(substitutions))
	})
	if (eq(nodetype Nodetype_raw){
		vecextend(getvar(code) arrget(getvar(node) 1))
		return(getvar(substitutions))
	})
	if (eq(nodetype Nodetype_block){
		#println("    {")
		!subcommands arrget(getvar(node) 1)
		veciter(subcommands varptr(item) {
			setvar(substitutions compile_tree(getvar(code) getvar(item) getvar(substitutions) getvar(scope)))
		})
		#println("    }")
		return(getvar(substitutions))
	})
	if (eq(nodetype Nodetype_macro){
		setvar(macrodata arrget(getvar(node) 2))
		return(treemapinsert(
			getvar(substitutions)
			arrget(getvar(node) 1)
			quadruple(
				arrget(getvar(macrodata) 2)
				getvar(substitutions)
				arrget(getvar(macrodata) 0)
				arrget(getvar(macrodata) 1)
			)
		))
	})
	if (eq(nodetype Nodetype_call){
		!_callnode_name arrget(getvar(node) 1)
		!_callnode_args arrget(getvar(node) 2)
		#print("    ")
		#printstr(_callnode_name)
		#println("(")
		setvar(cursub treemapget(getvar(substitutions) _callnode_name 0))
		if (eq(getvar(cursub) 0) {
			print("call to unknown macro '")
			printstr(_callnode_name)
			print("'\n")
			jump(@compile_tree_failed)
		})
		!_substitution_args arrget(getvar(cursub) 2)
		!_substitution_labels arrget(getvar(cursub) 3)
		!_substitution_body arrget(getvar(cursub) 0)
		!_substitution_substitutions arrget(getvar(cursub) 1)
		if (neq(veclength(_callnode_args) veclength(_substitution_args)) {
			print("macro call for '")
			printstr(_callnode_name)
			print("' has ")
			putint(veclength(_callnode_args))
			print(" arguments but macro has ")
			putint(veclength(_substitution_args))
			print(" arguments\n")
			jump(@compile_tree_failed)
		})
		setvar(bodysubs _substitution_substitutions)
		setvar(i 0)
		while(neq(getvar(i) veclength(_callnode_args)) {
			setvar(subcode vecnew)
			compile_tree(getvar(subcode) vecget(_callnode_args getvar(i)) getvar(substitutions) getvar(scope))
			.drop
			setvar(bodysubs treemapinsert(
				getvar(bodysubs)
				vecget(_substitution_args getvar(i))
				quadruple(
					quadruple(Nodetype_raw getvar(subcode) 0 arrget(getvar(node) 3))
					getvar(substitutions)
					vecnew
					vecnew
				)
			))
			addto(varptr(i) 1)
		})
		addto(@scopeid 1)
		
		veciter(_substitution_labels varptr(item) {
			setvar(subcode vecnew)
			vecappend(getvar(subcode) pair(Codetype_command Command_PUSH))
			setvar(name vecclone(getvar(item)))
			vecappend(getvar(name) Codetype_label)
			vecappend(getvar(name) deref(@scopeid))
			vecappend(getvar(subcode) pair(Codetype_reference getvar(name)))
			setvar(bodysubs treemapinsert(
				getvar(bodysubs)
				getvar(item)
				quadruple(
					quadruple(Nodetype_raw getvar(subcode) 0 arrget(getvar(node) 3))
					getvar(substitutions)
					vecnew
					vecnew
				)
			))
		})
		compile_tree(getvar(code) _substitution_body getvar(bodysubs) deref(@scopeid))
		.drop
		#println("    )")
		return(getvar(substitutions))
	})
	
	
	if (eq(nodetype Nodetype_num){
		vecappend(getvar(code) pair(Codetype_command Command_PUSH))
		vecappend(getvar(code) pair(Codetype_literal arrget(getvar(node) 1)))
		return(getvar(substitutions))
	})
	if (eq(nodetype Nodetype_reference){
		vecappend(getvar(code) pair(Codetype_command Command_PUSH))
		vecappend(getvar(code) pair(Codetype_reference arrget(getvar(node) 1)))
		return(getvar(substitutions))
	})
	if (eq(nodetype Nodetype_label){
		setvar(subcode vecclone(arrget(getvar(node) 1)))
		if(getvar(scope){
			vecappend(getvar(subcode) ':)
			vecappend(getvar(subcode) getvar(scope))
		})
		vecappend(getvar(code) pair(Codetype_label getvar(subcode)))
		return(getvar(substitutions))
	})
	
	print("Unknown AST node type '")
	putchar(nodetype)
	print("'")
	printint(nodetype)
	putnl
	
	:compile_tree_failed
	print("compiling the AST failed ")
	printint(arrget(getvar(node) 3))
	putnl
	exit(-1)
})

function (:main 0 5 {
	!input 2
	!tokens 3
	!token 4
	!ast 5
	!code 6
	
	setvar(input call(@readinput { }))
	putint(veclength(getvar(input)))
	println(" characters of input read")
	
	setvar(tokens tokenize(getvar(input)))
	veciter(getvar(tokens) varptr(token) {
		ifelse (eq(arrget(getvar(token) 0) Tokentype_ident) {
			printstr(arrget(getvar(token) 1))
		} {
			putchar(arrget(getvar(token) 0))
		})
		putchar(' )
	})
	putnl
	print("number of tokens: ")
	printint(veclength(getvar(tokens)))
	putnl
	
	print("memory used: ")
	putint(deref(@heapend))
	print(" words (")
	putint(div(deref(@heapend) 250))
	print(" KB)\n")
	
	setvar(ast parse(getvar(tokens)))
	print("ast type: ")
	putchar(deref(getvar(ast)))
	putnl
	
	print("memory used: ")
	putint(deref(@heapend))
	print(" words (")
	putint(div(deref(@heapend) 250))
	print(" KB)\n")
	
	setvar(code vecnew)
	compile_tree(getvar(code) getvar(ast) treemapnew 0)
	.drop
	println("compiling done")
	print("unlinked code tokens: ")
	putint(veclength(getvar(code)))
	putnl
	
	print("memory used: ")
	putint(deref(@heapend))
	print(" words (")
	putint(div(deref(@heapend) 250))
	print(" KB; ")
	putint(div(deref(@heapend) 250000))
	print(" MB)\n")
	
	0
})
# some extra space of memory
# this is used for global constants and variables
[0]
:scopeid # used by the compiler to keep assigning new unique scopes
[1 0]

!T16(n){n n n n n n n n n n n n n n n n}

[
:freetable
0
@endblock 0 0]
T16([0 @endblock 0 0])
T16([0 @endblock 0 0])




[0
:freeblocks_to6 # start of a linked list of free heap blocks
@endblock
0
0]
[0
:freeblocks_to10 # start of a linked list of free heap blocks
@endblock
0
0]
[0
:freeblocks_to100 # start of a linked list of free heap blocks
@endblock
0
0]
[0
:freeblocks_rest # start of a linked list of free heap blocks
@endblock
0
0]



:endblock
[0 0 0 0]
:heapstart # pointer to the first address that is used for heap memory
[@codeend]
:heapend # pointer to the first address that is not part of the used heap
[@codeend]
:funcmem # pointer to the location of the function arguments and local variables
[0]
:calltmp # hold the previous call frame when the stack is being manipulated
[0]

:log2table256 # see https://graphics.stanford.edu/~seander/bithacks.html#IntegerLogLookup
[-1 0 1 1 2 2 2 2 3 3 3 3 3 3 3 3]
T16([4]) T16([5]) T16([5]) T16([6]) T16([6]) T16([6]) T16([6])
T16([7]) T16([7]) T16([7]) T16([7]) T16([7]) T16([7]) T16([7]) T16([7])

[0]

# the stack begins here
:codeend
[0]


}
